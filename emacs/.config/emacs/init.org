#+title: Emacs Configuration
#+property: header-args :tangle "init.el"

* Installation

** Dependencies

*** Version
Before using this configuration, please ensure that Emacs v30.1 is installed. On Debian this can be achieved via [[https://backports.debian.org/Instructions/][backports]].

*** Font Installation
This configuration uses *Inter* and *JetBrains Mono*, which can be installed as follows:

#+begin_src sh :tangle no
mkdir -p ~/.local/share/fonts
cd ~/.local/share/fonts
wget https://github.com/rsms/inter/releases/download/v4.1/Inter-4.1.zip
unzip Inter-4.1.zip
wget https://download.jetbrains.com/fonts/JetBrainsMono-2.304.zip
unzip JetBrainsMono-2.304.zip
fc-cache -f -v
#+end_src
** Building
Follow [[https://www.masteringemacs.org/article/speed-up-emacs-libjansson-native-elisp-compilation][this tutorial]] and [[https://www.masteringemacs.org/article/how-to-get-started-tree-sitter][this tree-sitter add-on]], but make sure to use =--with-pgtk= to use Wayland.

Build tree-sitter first:
#+begin_src sh :tangle no
git clone git@github.com:tree-sitter/tree-sitter.git
cd tree-sitter/
make
sudo make install
#+end_src

You'll likely need something like this to ensure Emacs can find tree-sitter.
#+begin_src sh :tangle no
export LD_LIBRARY_PATH=/usr/local/lib/
#+end_src

#+begin_src sh :tangle no
git clone https://git.savannah.gnu.org/git/emacs.git # this will take a while
# make sure libgccjit-x-git matches gcc version
sudo apt-get install apt-transport-https ca-certificates curl gnupg-agent software-properties-common libgccjit0 libgccjit-12-dev libjansson4 libjansson-dev
cd emacs
./autogen.sh
./configure --without-compress-install --with-native-compilation --with-json --with-mailutils --with-pgtk --with-tree-sitter
make -j 4
sudo make install
#+end_src

** Setup
Ensure that no files exist at =~/.emacs= and =~/.emacs.d/=. Within the =~/dotfiles= directory, run the following:

#+begin_src sh :tangle no
killall emacs # stop any currently running emacs
stow -R --no-folding emacs -v
emacs # start emacs traditionally to ensure it installs correctly
#+end_src

** Android
Following [[https://hadi.timachi.com/posts/emacs_GUI_on_android/Use_Emacs_in_GUI_mode_on_Android_using_Termux_and_Termux_X11/][this guide]], with some modifications
#+begin_src sh :tangle no
pkg install x11-repo
pkg install termux-x11-nightly
#+end_src

Install [[https://github.com/termux/termux-x11/releases/tag/nightly][nightly Termux:X11]].

Set up dotfiles as above.

#+begin_src sh :tangle no
pkg install libwebp libxi emacs-x
termux-x11 :1 -xstartup "emacs"
#+end_src

* Configuration
** Lexical binding
#+begin_src emacs-lisp
;; -*- lexical-binding: t; -*-
#+end_src

** Custom File (1)
Customized variables via the Easy Customization Interface are saved here:

#+begin_src emacs-lisp
(setq custom-file "~/.config/emacs/emacs-custom.el")
#+end_src

This is loaded later in the configuration, at the end.

** Package Management

#+begin_src emacs-lisp
(setq package-archives '(("melpa" . "https://melpa.org/packages/")
			 ("melpa-stable" . "https://stable.melpa.org/packages/")
                         ("nongnu" . "https://elpa.nongnu.org/nongnu/")
			 ("org" . "https://orgmode.org/elpa/")
			 ("elpa" . "https://elpa.gnu.org/packages/")))
(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))

;; Initialize use-package
;; -p means "predicate"
(unless (package-installed-p 'use-package)
  (package-install 'use-package))
(require 'use-package)
;; Install all packages specified by use-package
(setq use-package-always-ensure t)
#+end_src

** Theme
This includes various graphical customizations, including fonts.

#+begin_src emacs-lisp
(setq inhibit-startup-message t)
(setq ring-bell-function 'ignore)
(menu-bar-mode -1)
(tool-bar-mode -1)
(scroll-bar-mode -1)
(blink-cursor-mode -1)
(setq-default display-line-numbers 'relative)
#+end_src

*** Font Settings

#+begin_src emacs-lisp
(use-package fontaine
  :init
  (setq fontaine-presets
	'((regular)
	  (large
	   :default-height 150)
	  (phone
	   :default-height 270)
	  (t
	   :default-family "JetBrains Mono"
	   :default-weight regular
	   :default-height 120

	   :variable-pitch-family "Inter"
	   )))
  (fontaine-set-preset 'regular)
  (fontaine-mode 1)
  :bind
  ("C-c f" . #'fontaine-set-preset))
#+end_src

For Android, automatically set some settings
#+begin_src emacs-lisp
(when (getenv "ANDROID_DATA")
  (toggle-frame-fullscreen)
  (fontaine-set-preset 'phone))
#+end_src

*** Theme Packages

#+begin_src emacs-lisp
;; run M-x nerd-icons-install-fonts after installing
(use-package nerd-icons)
(use-package nerd-icons-dired
  :hook
  (dired-mode . nerd-icons-dired-mode))

(use-package ef-themes
  :init
  (setq ef-themes-mixed-fonts t)
  (setq ef-themes-bold-constructs t)
  (setq ef-themes-italic-constructs t)
  (setq ef-themes-variable-pitch-ui nil)
  (setq ef-themes-prompts '(bold))
  (setq ef-themes-completions '((matches . (bold))
                                (selection . ())))
  (setq ef-themes-to-toggle '(ef-dream ef-kassio))
  (setq ef-themes-headings '((0 . (1.75))
                             (1 . (1.2))
                             (2 . (1.15))
                             (3 . (1.1))
                             (t . (1.05))))
  :config
  (ef-themes-load-theme 'ef-dream))
#+end_src

Shout out to [[https://github.com/protesilaos/spacious-padding/issues/9#issuecomment-2016613020][this GitHub comment]]
#+begin_src emacs-lisp
(use-package spacious-padding
  :config
  (spacious-padding-mode 1))
  
(defun spacious-padding-workaround ()
  "Workaround issues with `spacious-padding-mode' when using emacsclient."
  (when server-mode
    (spacious-padding-mode 1)))
(add-hook 'server-after-make-frame-hook #'spacious-padding-workaround)
#+end_src

*** Modeline
Shout out to [[https://protesilaos.com/codelog/2023-07-29-emacs-custom-modeline-tutorial/][this tutorial]]!

A helper function to only display a function if it is the current buffer.
#+begin_src emacs-lisp
(defun vitix/window (function)
  (when (mode-line-window-selected-p)
    (eval function)))
#+end_src


#+begin_src emacs-lisp
(defface vitix/modeline-highlighted-face
    `((t
       :background ,(ef-themes-get-color-value 'fg-alt)
       :foreground ,(ef-themes-get-color-value 'bg-main)
       :inherit bold))
    "Face for a highlighted background for the modeline")
#+end_src

The name of the current buffer.
#+begin_src emacs-lisp
(defvar-local vitix/modeline-buffer-name
    '(:eval (propertize (buffer-name) 'face 'bold)))
#+end_src

The current mode.
#+begin_src emacs-lisp
(defvar-local vitix/modeline-major-mode
    '(:eval
      (vitix/window
	'(propertize
	 (capitalize (replace-regexp-in-string "-mode" "" (symbol-name major-mode)))
	 'face 'bold))))
#+end_src

Whether the current buffer is in =meow-mode=.
#+begin_src emacs-lisp
(defvar-local vitix/modeline-meow-mode
    '(:eval
      (vitix/window
       '(propertize
	 (if (bound-and-true-p meow-mode) " M " "")
	 'face 'vitix/modeline-highlighted-face))))
#+end_src

Whether the current buffer is a file and has been modified.
#+begin_src emacs-lisp
(defvar-local vitix/modeline-buffer-modified
    '(:eval
      (vitix/window
       '(propertize (if (and (buffer-modified-p) (buffer-file-name)) " * " "")
	            'face 'vitix/modeline-highlighted-face))))
#+end_src

#+begin_src emacs-lisp
(defun vitix/eat-minor-mode ()
    (cond (eat--semi-char-mode " (semi-char)")
	  (eat--char-mode " (char)")
	  (eat--line-mode " (line)")
	  (t " (emacs)")
	  ))

(defvar-local vitix/modeline-eat-minor-mode
    '(:eval
      (vitix/window
       '(propertize
	 (if (string-equal (symbol-name major-mode) "eat-mode")
	   (vitix/eat-minor-mode)
	   "")))))
#+end_src

When you add a variable, be sure to add it to this list!
#+begin_src emacs-lisp
(dolist (var '(vitix/modeline-meow-mode
	       vitix/modeline-buffer-name
	       vitix/modeline-major-mode
	       vitix/modeline-eat-minor-mode
	       vitix/modeline-buffer-modified))
  (put var 'risky-local-variable t))
#+end_src

Set the mode line for real
#+begin_src emacs-lisp
(setq-default
 mode-line-format
 '("%e"
   vitix/modeline-meow-mode
   " "
   vitix/modeline-buffer-name
   " "
   vitix/modeline-buffer-modified
   mode-line-format-right-align
   vitix/modeline-major-mode
   vitix/modeline-eat-minor-mode
   "  "
   ))
#+end_src

** Undo Tree

#+begin_src emacs-lisp
  (setq make-backup-files nil)
  (use-package undo-tree
    :config
    (setq undo-tree-history-directory-alist '(("." . "~/.cache/emacs/undo/")))
    :init
    (global-undo-tree-mode))
#+end_src

** Authentication

#+begin_src emacs-lisp
(setq auth-sources '("~/.authinfo.gpg"))
#+end_src

Shout out to [[https://www.reddit.com/r/emacs/comments/z12926/comment/j33zcsw/?utm_source=share&utm_medium=web3x&utm_name=web3xcss&utm_term=1&utm_content=share_button][this post]]!
#+begin_src emacs-lisp
(defun vitix/disable-backups () 
  "Disable backups and autosaving for files ending in \".gpg\" or those in \"/dev\"."
  (when (and (buffer-file-name) 
             (or (string-match "\\.gpg\\'" (buffer-file-name))
		 (string-match "^/dev" (buffer-file-name)))) 
    (setq-local backup-inhibited t) 
    (setq-local undo-tree-auto-save-history nil) 
    (auto-save-mode -1))) 
(add-hook 'find-file-hook #'vitix/disable-backups) 
#+end_src

#+begin_src emacs-lisp
(setq history-add-new-input nil)
#+end_src

** Meow
???

#+begin_src emacs-lisp
(defun meow-setup ()
  (setq meow-cheatsheet-layout meow-cheatsheet-layout-colemak-dh)
  (meow-motion-define-key
   ;; Use e to move up, n to move down.
   ;; Since special modes usually use n to move down, we only overwrite e here.
   '("e" . meow-prev)
   '("<escape>" . ignore))
  (meow-leader-define-key
   '("?" . meow-cheatsheet)
   '("1" . meow-digit-argument)
   '("2" . meow-digit-argument)
   '("3" . meow-digit-argument)
   '("4" . meow-digit-argument)
   '("5" . meow-digit-argument)
   '("6" . meow-digit-argument)
   '("7" . meow-digit-argument)
   '("8" . meow-digit-argument)
   '("9" . meow-digit-argument)
   '("0" . meow-digit-argument))
  (meow-normal-define-key
   '("0" . meow-expand-0)
   '("1" . meow-expand-1)
   '("2" . meow-expand-2)
   '("3" . meow-expand-3)
   '("4" . meow-expand-4)
   '("5" . meow-expand-5)
   '("6" . meow-expand-6)
   '("7" . meow-expand-7)
   '("8" . meow-expand-8)
   '("9" . meow-expand-9)
   '("-" . negative-argument)
   '(";" . meow-reverse)
   '("," . meow-inner-of-thing)
   '("." . meow-bounds-of-thing)
   '("[" . meow-beginning-of-thing)
   '("]" . meow-end-of-thing)
   '("/" . meow-visit)
   '("a" . meow-append)
   '("A" . meow-open-below)
   '("b" . meow-back-word)
   '("B" . meow-back-symbol)
   '("c" . meow-change)
   '("e" . meow-prev)
   '("E" . meow-prev-expand)
   '("f" . meow-find)
   '("g" . meow-cancel-selection)
   '("G" . meow-grab)
   '("m" . meow-left)
   '("M" . meow-left-expand)
   '("i" . meow-right)
   '("I" . meow-right-expand)
   '("j" . meow-join)
   '("k" . meow-kill)
   '("l" . meow-line)
   '("L" . meow-goto-line)
   '("h" . meow-mark-word)
   '("H" . meow-mark-symbol)
   '("n" . meow-next)
   '("N" . meow-next-expand)
   '("o" . meow-block)
   '("O" . meow-to-block)
   '("p" . meow-yank)
   '("q" . meow-quit)
   '("r" . meow-replace)
   '("s" . meow-insert)
   '("S" . meow-open-above)
   '("t" . meow-till)
   '("u" . meow-undo)
   '("U" . meow-undo-in-selection)
   '("v" . meow-search)
   '("w" . meow-next-word)
   '("W" . meow-next-symbol)
   '("x" . meow-delete)
   '("X" . meow-backward-delete)
   '("y" . meow-save)
   '("z" . meow-pop-selection)
   '("'" . repeat)
   '("<escape>" . ignore)))

(use-package meow
  :init
  (setq meow-expand-hint-remove-delay 0)
  :config
  (meow-setup)
  (meow-global-mode 1))
#+end_src

** Terminal

Eat doesn't have the best fish integration, but it treats input much better. It's also written in elisp, so I don't need to install =libtool-bin= or compile anything.
#+begin_src emacs-lisp
(defun vitix/send-escape ()
  (interactive)
  (eat--send-input nil (kbd "ESC")))

(use-package eat
  :bind
  ("C-c t s" . #'eat-semi-char-mode)
  ("C-c t e" . #'eat-emacs-mode)
  ("C-<escape>" . #'vitix/send-escape)
  ("C-g" . #'vitix/send-escape))
#+end_src
** Completion

My completion stack is as follows:
- vertico
- consult
- marginalia
- orderless

#+begin_src emacs-lisp
(use-package vertico
  :init
  (vertico-mode 1)
  (savehist-mode 1)
  (add-hook 'rfn-eshadow-update-overlay-hook #'vertico-directory-tidy))

(use-package consult)

(use-package marginalia
  :init
  (marginalia-mode 1))

(use-package orderless
  :custom
  (completion-styles '(orderless basic))
  (completion-category-overrides '((file (styles basic partial-completion)))))
#+end_src


** Dired
#+begin_src emacs-lisp
(add-hook 'dired-mode-hook #'dired-hide-details-mode)
#+end_src

** Magit
#+begin_src emacs-lisp
(use-package magit)
#+end_src

** Org Mode
#+begin_src emacs-lisp
(defun vitix/org-mode-setup ()
  (variable-pitch-mode)
  (visual-line-mode)
  (org-indent-mode)
  )
(use-package org
  :hook (org-mode . vitix/org-mode-setup)
  :config
  (setq org-hide-emphasis-markers t)
  (setq org-src-preserve-indentation t)
  (setq org-return-follows-link t)
  (setq org-startup-truncated nil)
  (setq org-directory "~/tome")
  :bind
  ("C-c h" . #'org-fold-hide-subtree)
  ("C-c s" . #'org-fold-show-subtree))
#+end_src

This package emulates a WYSIWYG editor. More options can be found on [[https://github.com/awth13/org-appear][GitHub]].
#+begin_src emacs-lisp
(use-package org-appear
  :init
  (add-hook 'org-mode-hook 'org-appear-mode)
  )
#+end_src

Special UTF-8 headings:
#+begin_src emacs-lisp
(use-package org-bullets
  :after org
  :hook (org-mode . org-bullets-mode)
  :custom
  (org-bullets-bullet-list '("◉" "○" "●" "○" "●" "○" "●")))
#+end_src

Using =C-c C-,= I can insert code blocks with the following values:
#+begin_src emacs-lisp
(setq org-structure-template-alist '(("s" . "src")
                                     ("e" . "src emacs-lisp")
                                     ("p" . "src python")
				     ("t" . "src sh :tangle no")))
#+end_src

*** Org Capture
[[https://orgmode.org/manual/Capture.html][Documentation]]

#+begin_src emacs-lisp
(use-package org-capture
  :ensure nil ; org-capture comes with emacs, just use this to configure it
  :config
  (setq org-capture-templates
        '(("l" "Log" entry
           (file+headline denote-journal-path-to-new-or-existing-entry "Log")
           "* %<%I:%M %p> - %?"
           )
          ("t" "Task" entry
           (file+headline denote-journal-path-to-new-or-existing-entry "Task")
           "* TODO %?"
           )
          ("i" "TTRPG Idea" entry
           (file+headline "20250507T140321--ttrpg-ideas__ttrpg.org" "Ideas")
           "* %?")))
  :bind
  ("C-c c" . org-capture))
#+end_src

*** Org Agenda

In the future, I'd like to make a more in-depth agenda config.

#+begin_src emacs-lisp
(setq org-todo-keywords '((sequence
                           "TODO(t)"
                           "WAIT(w@/!)"
                           "|"
                           "DONE(d/!)"
                           "STOP(s@/!)")))
(setq org-todo-keyword-faces
      `(("TODO" . ,(ef-themes-get-color-value 'green))
	("WAIT" . ,(ef-themes-get-color-value 'yellow-warmer))
	("DONE" . ,(ef-themes-get-color-value 'bg-dim))
	("STOP" . ,(ef-themes-get-color-value 'fg-dim))))
(set-face-attribute 'org-headline-done nil
		    :foreground (ef-themes-get-color-value 'bg-dim))

(use-package org-agenda
  :ensure nil
  :config
  (setq org-agenda-files (list org-directory))
  :bind
  ("C-c a" . org-agenda))
#+end_src

** Denote
Let's try simplicity.

#+begin_src emacs-lisp
(use-package denote
  :hook (dired-mode . denote-dired-mode)
  :bind
  (("C-c n n" . denote)
   ("C-c n r" . denote-rename-file)
   ("C-c n l" . denote-link)
   ("C-c n d" . denote-dired)
   ("C-c n g" . denote-grep))
  :config
  (setq denote-directory (expand-file-name "~/tome"))
  (denote-rename-buffer-mode 1)
  (setq denote-known-keywords '()))
#+end_src


Useful functions for searching through Denote entries.
#+begin_src emacs-lisp
(use-package consult-denote
  :bind
  (("C-c n f" . consult-denote-find)
   ("C-c n g" . consult-denote-grep))
  :config
  (consult-denote-mode 1))
#+end_src

Useful functions for journaling in Denote.
#+begin_src emacs-lisp
(use-package denote-journal
  :hook (calendar-mode . denote-journal-calendar-mode)
  :bind
  (("C-c n t" . denote-journal-new-or-existing-entry)
   ("C-c n s" . denote-journal-link-or-create-entry))
  :config
  ;; save journal entries in denote-directory
  (setq denote-journal-directory nil)
  (setq denote-journal-keyword "journal")
  (setq denote-journal-title-format 'day-date-month-year)
  )
#+end_src

Useful functions for Denote with Org mode.
#+begin_src emacs-lisp
(use-package denote-org)
#+end_src


** Publishing

A package to export an Org file into GitHub-style Markdown. Particularly, this uses the backtick codeblock syntax, which the built-in =org-md-export-to-markdown= does not do.

#+begin_src emacs-lisp
(use-package ox-gfm)
#+end_src

I wrote basically a whole package to export my files to Jekyll! Simply call =vitix/publish= and all Denote files with the =publish= tag will get published.

#+begin_src emacs-lisp
(defun vitix/publish--files (tag)
  "Returns a list of files with the given tag."
  (seq-filter (lambda (full-filename)
		(string-match (concat "_" tag) full-filename))	    
              (directory-files denote-directory t)))

(defun vitix/publish--export (full-filename)
  "Exports an Org file to GitHub Markdown and returns the full file name."
  (with-current-buffer (find-file-noselect full-filename)
    (org-gfm-export-to-markdown))
  (concat
   (file-name-directory full-filename)
   (file-name-base full-filename)
   ".md"))

(defun vitix/publish--filename-date (filename)
  "Returns the Jekyll filename date section of a Denote file."
  (concat
   (substring filename 0 4)
   "-"
   (substring filename 4 6)
   "-"
   (substring filename 6 8)))

(defun vitix/publish--filename-title (filename)
  "Returns the Jekyll filename title section of a Denote file."
  (substring filename
	     (+ (string-search "--" filename) 2)
	     (string-search "__" filename)))

(defun vitix/publish--filename (full-filename)
  "Returns the Jekyll file name of a Denote file."
  (let ((filename (file-name-nondirectory full-filename)))
    (concat (vitix/publish--filename-date filename)
	    "-"
	    (vitix/publish--filename-title filename)
	    ".md")))

(defun vitix/publish--tags (filename)
  "Returns a list of tags of a Denote file."
  (string-split (substring filename
			   (+ (string-search "__" filename) 2)
			   (string-search "." filename))
		"_"))

(defun vitix/publish--org-title (full-filename)
  "Extracts the original title from an Org file."
  (with-temp-buffer
    (insert-file-contents full-filename)
    (org-mode)
    (cadr (car (org-collect-keywords '("TITLE"))))))

(defun vitix/publish--metadata (full-filename)
  "Returns the Jekyll metadata about a Denote file."
  (concat "---\n"
	  "title: "
	  (vitix/publish--org-title full-filename)
	  "\n"

	  "tags: ["
	  (string-join
	   (vitix/publish--tags
	    (file-name-nondirectory full-filename))
	   ",")
	  "]\n"

	  "---\n"))

(defun vitix/publish--add-metadata (original-full-filename export-full-filename)
  "Writes the metadata to the exported file."
  (write-region (concat (vitix/publish--metadata original-full-filename)
	                (vitix/load-file-as-string export-full-filename))
		nil
		export-full-filename))

(defun vitix/load-file-as-string (full-filename)
  "Return the contents of the file as a string."
  (with-temp-buffer
    (insert-file-contents full-filename)
    (buffer-string)))

(defun vitix/publish--link (full-filename)
  "Return the Jekyll link of a Denote file."
  (concat "/posts/" (vitix/publish--filename-title full-filename)))

(defun vitix/publish--filter-links (full-filename)
  "Replace all Denote links with Jekyll links."
  (let* ((text (vitix/load-file-as-string full-filename))
	 (link-index (string-search denote-directory text)))
    ;; loop until there's no more
    (while link-index
      (let ((link-full-filename
	     (substring text link-index (string-search ")" text link-index))))
	(setq text (replace-regexp-in-string
		    link-full-filename
		    (vitix/publish--link link-full-filename)
		    text)))
      (setq link-index (string-search denote-directory text)))
    (write-region text nil full-filename)))

(defun vitix/publish (&optional tag export-directory)
  "Publish Denote files to the export directory."
  (interactive)
  (unless tag
    (setq tag "publish"))
  (unless export-directory
    (setq export-directory "~/projects/technologeli.github.io/_posts/"))
  (dolist (full-filename (vitix/publish--files tag))
    (let ((export-full-filename (concat export-directory
					(vitix/publish--filename full-filename))))
      (rename-file (vitix/publish--export full-filename)
		   export-full-filename
		   t)
      (vitix/publish--filter-links export-full-filename)
      (vitix/publish--add-metadata full-filename export-full-filename))))
#+end_src

** Development
*** LSP (Eglot)

Most language servers are placed in =~/.local/bin= or installed via NPM. For me, that places them into =~/.volta/bin=.

Check that the language server can be found with =executable-find= or that its path is in
=exec-path=.

#+begin_src emacs-lisp
(add-to-list 'exec-path "/home/eli/.volta/bin")
(add-to-list 'exec-path "/home/eli/.local/bin")
#+end_src

If =tramp-own-remote-path= is in =tramp-remote-path=, Tramp will look in the =PATH= of =~/.profile=.

This only works after loading SSH.
#+begin_src emacs-lisp :tangle no
(add-to-list 'tramp-remote-path 'tramp-own-remote-path)
#+end_src

I'm using =eglot=, which feels much more integrated and lightweight than =lsp-mode=.
#+begin_src emacs-lisp
(use-package eglot
  :ensure nil
  :config
  (global-eldoc-mode t)
  :bind
  (("C-c e r" . #'eglot-rename)
  ("C-c e f n" . #'flymake-goto-next-error)
  ("C-c e f p" . #'flymake-goto-prev-error)
  ("C-c e c" . #'eglot-code-actions)
  ("C-c e d" . #'xref-find-definitions)
  ("C-c e k" . #'eldoc)))

#+end_src

*** Completion
#+begin_src emacs-lisp
(use-package corfu
  :bind (:map corfu-map
              ("M-SPC" . corfu-insert-separator)
              ("M-y" . corfu-insert)
              ("RET" . nil))
  :init
  (global-corfu-mode t)
  (corfu-history-mode t))
#+end_src

#+begin_src emacs-lisp
(use-package cape
  :bind ("C-c p" . cape-prefix-map)
  :init
  (advice-add 'eglot-completion-at-point :around #'cape-wrap-buster)
  (add-hook 'completion-at-point-functions #'cape-file)
  (add-hook 'completion-at-point-functions #'cape-elisp-block)
  )
#+end_src

** Keybinds / Keymaps

#+begin_src emacs-lisp
(which-key-mode 1)
(defvar-keymap vitix/harpoon-keymap
  :doc "Harpoon, but its actually bookmarks"
  "s" #'bookmark-save
  "l" #'bookmark-load
  "f" #'consult-bookmark
  "d" #'bookmark-delete)

(defvar-keymap vitix/prefix-keymap
  :doc "My custom keymap!"
  "b" #'consult-buffer
  "t" #'eat
  "-" #'dired-jump
  "S-t" #'ef-themes-toggle
  "e" #'eglot
  "z" #'meow-global-mode
  "h" vitix/harpoon-keymap)

(keymap-set global-map "C-t" vitix/prefix-keymap)
(global-set-key (kbd "C-o") #'other-window)
(define-key dired-mode-map (kbd "-") #'dired-up-directory)
#+end_src

Replace =C-v= and =M-v= with scrolling half-page forward and backward.
#+begin_src emacs-lisp
(use-package view
  :ensure nil
  :config
  (define-key global-map (kbd "C-v") #'View-scroll-half-page-forward)
  (define-key global-map (kbd "M-v") #'View-scroll-half-page-backward)
  )
#+end_src

** Custom File (2)
This line must always be kept at the end so customizations stay.

#+begin_src emacs-lisp
(load custom-file)
#+end_src

